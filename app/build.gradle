apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-allopen'
apply plugin: 'shot'

allOpen {
    // allows mocking for classes w/o directly opening them for release builds
    annotation 'io.github.erikjhordanrey.atp_rankings_di.core.testing.OpenClass'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "io.github.erikjhordanrey.atp_rankings_di"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "io.github.erikjhordanrey.atp_rankings_di.runner.HiltTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
    }

    sourceSets.forEach {
        it.java.srcDirs = ["src/$it.name/kotlin"]
    }

    sourceSets {
        test.java.srcDirs += "src/sharedTest/kotlin"
        androidTest.java.srcDirs += "src/sharedTest/kotlin"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation "androidx.activity:activity-ktx:1.1.0"

    implementation "androidx.lifecycle:lifecycle-viewmodel:2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9-native-mt-2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9-native-mt"

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha02'

    implementation "com.google.dagger:hilt-android:2.29.1-alpha"
    kapt "com.google.dagger:hilt-android-compiler:2.29.1-alpha"

    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.29.1-alpha'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.29.1-alpha'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.9-native-mt-2"

    androidTestImplementation ("com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0") {
        exclude group: "org.mockito", module: "mockito-core"
    }
    androidTestImplementation 'org.mockito:mockito-android:3.5.13'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.3.0"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.2"
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
}
